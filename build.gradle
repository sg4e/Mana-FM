plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

javafx {
    version = "14"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
}

configurations.all {
    if (file(LOCAL_REPO_DIR).exists()) {
        resolutionStrategy.dependencySubstitution {
            substitute module(REMOTE_GAMEDATA_REPO) with project(LOCAL_GAMEDATA_REPO)
        }
    }
}

dependencies {
    implementation REMOTE_GAMEDATA_REPO
    implementation 'org.controlsfx:controlsfx:11.0.1'
    implementation group: 'com.github.afester.javafx', name: 'FranzXaver', version: '0.1'
    
    //lombok
    compileOnly 'org.projectlombok:lombok:1.18.8'
    annotationProcessor 'org.projectlombok:lombok:1.18.8'
    
    //logging
    implementation group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.26'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.1'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.1'
    annotationProcessor(group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.1')
    
    //cross-platform runtimes
    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:win"
    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:linux"
    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:mac"
    
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.6.2'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.2'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.6.2'
    testImplementation "org.testfx:testfx-junit5:4.0.16-alpha"
    testImplementation 'org.mockito:mockito-core:3.5.10'
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked"
    options.compilerArgs << "-Xlint:deprecation"
}

mainClassName = 'sg4e.ygofm.mana.Launcher'

application {
    applicationDefaultJvmArgs = ['--add-exports', 'javafx.base/com.sun.javafx.event=ALL-UNNAMED']
}

jar {
    manifest {
        attributes 'Main-Class': 'sg4e.ygofm.mana.Launcher',
                   "Multi-Release": true
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

test {
    useJUnitPlatform()
}
